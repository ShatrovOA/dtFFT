!------------------------------------------------------------------------------------------------
! Copyright (c) 2021, Oleg Shatrov
! All rights reserved.
! This file is part of dtFFT library.

! dtFFT is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.

! dtFFT is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.

! You should have received a copy of the GNU General Public License
! along with this program.  If not, see <https://www.gnu.org/licenses/>.
!------------------------------------------------------------------------------------------------
module dtfft_interface_nvrtc
!! nvRTC Interfaces
use iso_c_binding
use dtfft_parameters, only: dtfft_stream_t
use dtfft_interface_cuda_runtime, only: dim3
implicit none
private
public :: nvrtcGetErrorString

  interface nvrtcGetErrorString_c
  !! Helper function that returns a string describing the given nvrtcResult code
  !! For unrecognized enumeration values, it returns "NVRTC_ERROR unknown"
    function nvrtcGetErrorString_c(error_code)                                              &
      result(string)                                                                        &
      bind(C, name="nvrtcGetErrorString")
    import
      integer(c_int),  value  :: error_code !! CUDA Runtime Compilation API result code.
      type(c_ptr)             :: string     !! Pointer to C string
    end function nvrtcGetErrorString_c
  end interface

public :: nvrtcCreateProgram
  interface nvrtcCreateProgram
  !! Creates an instance of nvrtcProgram with the given input parameters, 
  !! and sets the output parameter prog with it.
    function nvrtcCreateProgram(prog, src, name, numHeaders, headers, includeNames)         &
      result(nvrtcResult)                                                                   &
      bind(C, name="nvrtcCreateProgram")
    import
      type(c_ptr)           :: prog         !! CUDA Runtime Compilation program.
      character(c_char)     :: src(*)       !! CUDA program source.
      character(c_char)     :: name(*)      !! CUDA program name.
      integer(c_int), value :: numHeaders   !! Number of headers used. Must be greater than or equal to 0.
      type(c_ptr),    value :: headers      !! Sources of the headers
      type(c_ptr),    value :: includeNames !! Name of each header by which they can be included in the CUDA program source
      integer(c_int)        :: nvrtcResult  !! The enumerated type nvrtcResult defines API call result codes.
    end function nvrtcCreateProgram
  end interface

public :: nvrtcDestroyProgram
  interface nvrtcDestroyProgram
  !! Destroys the given program.
    function nvrtcDestroyProgram(prog)                                                      &
      result(nvrtcResult)                                                                   &
      bind(C, name="nvrtcDestroyProgram")
    import
      type(c_ptr)           :: prog         !! CUDA Runtime Compilation program.
      integer(c_int)        :: nvrtcResult  !! The enumerated type nvrtcResult defines API call result codes.
    end function nvrtcDestroyProgram
  end interface

public :: nvrtcCompileProgram
  interface nvrtcCompileProgram
  !! Compiles the given program.
    function nvrtcCompileProgram(prog, numOptions, options)                                 &
      result(nvrtcResult)                                                                   &
      bind(C, name="nvrtcCompileProgram")
    import
      type(c_ptr),    value :: prog         !! CUDA Runtime Compilation program.
      integer(c_int), value :: numOptions   !! Number of compiler options passed.
      type(c_ptr)           :: options(*)   !! Compiler options in the form of C string array
      integer(c_int)        :: nvrtcResult  !! The enumerated type nvrtcResult defines API call result codes.
    end function nvrtcCompileProgram
  end interface

public :: nvrtcGetProgramLog
  interface nvrtcGetProgramLog
  !! Stores the log generated by the previous compilation of prog in the memory pointed by log
    function nvrtcGetProgramLog(prog, log)                                                  &
      result(nvrtcResult)                                                                   &
      bind(C, name="nvrtcGetProgramLog")
    import
      type(c_ptr),    value :: prog         !! CUDA Runtime Compilation program.
      type(c_ptr),    value :: log          !! Compilation log.
      integer(c_int)        :: nvrtcResult  !! The enumerated type nvrtcResult defines API call result codes.
    end function nvrtcGetProgramLog
  end interface

public :: nvrtcGetCUBINSize
  interface nvrtcGetCUBINSize
  !! Sets the value of ``cubinSizeRet`` with the size of the cubin generated by the previous compilation of ``prog``.
    function nvrtcGetCUBINSize(prog, cubinSizeRet)                                          &
      result(nvrtcResult)                                                                   &
      bind(C, name="nvrtcGetCUBINSize")
    import
      type(c_ptr),    value :: prog         !! CUDA Runtime Compilation program.
      integer(c_size_t)     :: cubinSizeRet !! Size of the generated cubin.
      integer(c_int)        :: nvrtcResult  !! The enumerated type nvrtcResult defines API call result codes.
    end function nvrtcGetCUBINSize
  end interface

public :: nvrtcGetCUBIN
  interface nvrtcGetCUBIN
  !! Stores the cubin generated by the previous compilation of ``prog`` in the memory pointed by ``cubin``.
    function nvrtcGetCUBIN(prog, cubin)                                                     &
      result(nvrtcResult)                                                                   &
      bind(C, name="nvrtcGetCUBIN")
    import
      type(c_ptr),    value :: prog         !! CUDA Runtime Compilation program.
      character(c_char)     :: cubin(*)     !! Compiled and assembled result.
      integer(c_int)        :: nvrtcResult  !! The enumerated type nvrtcResult defines API call result codes.
    end function nvrtcGetCUBIN
  end interface




contains

  function nvrtcGetErrorString(error_code) result(string)
  !! Helper function that returns a string describing the given nvrtcResult code
  !! For unrecognized enumeration values, it returns "NVRTC_ERROR unknown"
    integer(c_int),   intent(in)  :: error_code     !! CUDA Runtime Compilation API result code.
    character(len=:), allocatable :: string         !! Result string
    type(c_ptr)                   :: c_string       !! Pointer to C string
    character(len=256), pointer   :: f_string       !! Pointer to Fortran string

    c_string = nvrtcGetErrorString_c(error_code)
    call c_f_pointer(c_string, f_string)
    allocate( string, source=f_string(1:index(f_string, c_null_char) - 1) )
  end function nvrtcGetErrorString
end module dtfft_interface_nvrtc